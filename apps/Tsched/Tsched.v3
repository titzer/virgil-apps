// Representation of tasks as a graph.
var ID: int;
class Task(name: string) {
	var id = ID++;
	var dependencies: List<Task>;
	var state: int;
	def removeDependent(t: Task) {
		dependencies = removeRec(t, dependencies);
	}
	def addDependent(t: Task) {
		dependencies = List.new(t, dependencies);
	}
	private def removeRec(t: Task, list: List<Task>) -> List<Task> {
		if (list == null) return null;
		var o = list.tail, n = removeRec(t, list);
		if (list.head == t) return n;
		if (o != n) return List.new(list.head, n);
		return list;
	}
}

class TaskParser(input: Array<byte>) {
	def map = Strings.newMap<Task>();
	def parseLine(input: Array<byte>, pos: int) -> Task {
		return null;
	}

	def parse() -> List<Task> {
		return null;
	}
}

def main(args: Array<string>) -> int {
	for (filename in args) {
		if (filename.length > 0 && filename[0] == '-') continue;
		var input = System.fileLoad(filename);
		if (input == null) {
			System.error("FileNotFound", filename);
			return 1;
		}
		var parser = TaskParser.new(input);
		var list = parser.parse();
	}

	testTasks();

	return 0;
}


def testTasks() {
	var A = Task.new("A");
	var B = Task.new("B");
	var C = Task.new("C");
	var D = Task.new("D");

	A.addDependent(B);
	A.addDependent(C);
	B.addDependent(D);
	C.addDependent(D);
//	D.addDependent(A);

	var list = Lists.fromArray([A, B, C, D]);

//	printList(RecursiveSolutionManual.new().schedule(list));	
	printList(ReverseGraphSolution.new().schedule(list));	
}

def printList(list: List<Task>) {
	for (l = list; l != null; l = l.tail) {
		System.puts(l.head.name);
		System.ln();
	}
}

def cycleDetected() {
	System.error("InputError", "cycle detected");
}

// --- Standard recursive solution ------------------------------------
class RecursiveSolution {
	def UNVISITED = 0;
	def ON_STACK = 1;
	def SCHEDULED = 2;
	var solution: List<Task>;
	def schedule(list: List<Task>) -> List<Task> {
		var result: List<Task>;
		solution = null;
		for (l = list; l != null; l = l.tail) recurse(l.head);
		return Lists.reverse(solution);
	}
	def recurse(t: Task) {
		if (t.state == SCHEDULED) return;
		if (t.state == ON_STACK) cycleDetected();
		t.state = ON_STACK;
		for (d = t.dependencies; d != null; d = d.tail) recurse(d.head);
		t.state = SCHEDULED;
		solution = List.new(t, solution);
	}
}

// --- Standard DFS, manual recursion ---------------------------------
class RecursiveSolutionManual {
	def UNVISITED = 0;
	def ON_STACK = 1;
	def SCHEDULED = 2;
	def schedule(list: List<Task>) -> List<Task> {
		var solution: List<Task>;
		var stack = Vector<(Task, List<Task>)>.new();
		stack.put(null, list);
		while (stack.length > 0) {
			var e = stack[stack.length - 1];
			if (e.1 == null) {
				var t = e.0;
				stack.length--;
				if (t == null) continue;
				t.state = SCHEDULED;
				solution = List.new(t, solution);
			} else {
				stack[stack.length - 1] = (e.0, e.1.tail);
				var t = e.1.head;
				if (t.state == ON_STACK) cycleDetected();
				if (t.state == SCHEDULED) continue;
				stack.put((t, t.dependencies));
				t.state = ON_STACK;
			}
		}
		return Lists.reverse(solution);
	}
}


// --- ReverseGraph solution ------------------------------------
class RevTask(task: Task) {
	var count: int;
	var uses: List<RevTask>;
	def put(r: RevTask) {
		r.count++;
		uses = List.new(r, uses);
	}
}
class ReverseGraphSolution {
	def schedule(list: List<Task>) -> List<Task> {
		var q = Vector<RevTask>.new();
		rev(list, q, HashMap<Task, RevTask>.new(Task.id, Task.==));
		var count = q.length, sched = 0;
		var result: List<Task>;
		for (i = 0; i < q.length; i++) {
			var tr = q[i];
			if (tr.count == 0) {
				result = List.new(tr.task, result);
				for (l = tr.uses; l != null; l = l.tail) {
					if (--l.head.count == 0) q.put(l.head);
				}
				tr.count = -1;
				sched++;
			}
		}
		if (sched != count) cycleDetected();
		return Lists.reverse(result);
	}
	def rev(list: List<Task>, q: Vector<RevTask>, map: HashMap<Task, RevTask>) {
		for (l = list; l != null; l = l.tail) {
			var t = l.head;
			if (map.has(t)) continue;
			var tr = RevTask.new(t);
			q.put(tr);
			map[t] = tr;
			rev(t.dependencies, q, map);
			for (d = t.dependencies; d != null; d = d.tail) {
				var dr = map[d.head];
				dr.put(tr);
			}
		}
	}
}

class PriorityQueueSolution {
}
