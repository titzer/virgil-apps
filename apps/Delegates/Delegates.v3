// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

class del {
	def add(a: int, b: int) -> int {
		return a + b + 3;
	}
}
component Delegates {
	var count: int;
	var iters: int;
	def main(args: Array<string>) -> int {
		if (args.length < 2) {
			System.puts("usage: Delegates <count> <iters>\n");
			return 1;
		}
		count = Int.parse(args[0]);
		iters = Int.parse(args[1]);
		var o = del.new();
		for (i < iters) {
			var f: (int, int) -> int;
			match (i & 0x3) {
				0 => f = int.+;
				1 => f = sub;
				2 => f = add<int>;
				3 => f = o.add;
			}
			apply(f);
		}
		return 0;
	}
	def apply(f: (int, int) -> int) -> int {
		var x = 334;
		for (i < count) x = f(x, 17);
		return x;
	}
	def sub(a: int, b: int) -> int {
		return a - b;
	}
	def add<T>(a: int, b: T) -> int {
		return a + 13;
	}
}
