// Copyright 2020 Ben L. Titzer. All rights reserved.

// Virgil implementation of the "mandelbrot" benchmark from the Computer Language Shootout.
// A straightforward translation from the C version.

def LIMIT = 4d;
def ITER = 50;

def main(args: Array<string>) -> int {
	var w = Util.intArg(args, 1), h = w;
	System.puts("P4\n");
	System.puti(w);
	System.puts(" ");
	System.puti(h);
	System.ln();

	var Cr = 0.0d, Ci = 0.0d;
	var byte_acc: byte = 0;
	var bit_num = 0;

	for (y < h) {
		for (x < w) {
			var Zr = 0.0d, Zi = 0.0d, Tr = 0.0d, Ti = 0.0d;
			Cr = (2.0 * x / w - 1.5);
			Ci = (2.0 * y / h - 1.0);

			for (i = 0; i < ITER && (Tr + Ti <= LIMIT); ++i) {
				Zi = 2.0 * Zr * Zi + Ci;
				Zr = Tr - Ti + Cr;
				Tr = Zr * Zr;
				Ti = Zi * Zi;
			}

			byte_acc <<= 1;
			if(Tr + Ti <= LIMIT) byte_acc |= 0x01;

			++bit_num;

			if (bit_num == 8) {
				System.putc(byte_acc);
				byte_acc = 0;
				bit_num = 0;
			} else if(x == w - 1) {
				byte_acc <<= u5.view(8 - w & 7);
				System.putc(byte_acc);
				byte_acc = 0;
				bit_num = 0;
			}
		}
	}

	return 0;
}
